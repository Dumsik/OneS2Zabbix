
#Область ОбработчикиСобытий

// Обработчик предназначен для проверки доступности HTTP служб
// Вызывается обработчик относительно редко, повторноое использование
// сеансов не устанавливается.
// Результат работы: 
// 	успех: http - 200, строка "ОК" в теле запроса (Content-Type: text/plain)
//
Функция Пинг(Запрос)
	
	ОтветСтрокой = "OK";
	
	Ответ = Новый HTTPСервисОтвет(200);
	Ответ.Заголовки.Вставить("Content-Type", "text/plain");	
	Ответ.УстановитьТелоИзСтроки(ОтветСтрокой);
	
	Возврат Ответ;
	
КонецФункции

// Обработчик (POST) предназначен для произвольной обработки запросов, поступающих от Zabbix.
// URL содержит элемент (type) обработчика, который соответствует определенному обработчику 1С.
// Если тип обработчика неизвестен, сервис возвращает HTTP 500
// Все дополнительные параметры передаются в теле запроса в формате JSON
// Результат выполнения обработчиков запроса всегда HTTP 200. В случае невозможности успешного
// завершения обработчика, бросается исключение с описанием ошибки (система сгенерирует HTTP 500)
//
Функция СлужебныеСервисы(Запрос)
	
	ТипОбработчика = Запрос.ПараметрыURL.Получить("type");
	
	ТелоЗапросаСтрокой = Запрос.ПолучитьТелоКакСтроку();
	
	Если ВРег(ТипОбработчика) = "ECHO" Тогда
		Ответ = Эхо(ТелоЗапросаСтрокой);
	ИначеЕсли ВРег(ТипОбработчика) = "MINTOTALSPERIOD" Тогда 
		Ответ = МинимальнаяДатаИтоговРегистровНакопленияИБухгалтерии();
	Иначе
		ВызватьИсключение СтрШаблон("Отсутсвует обработчик с типом [%1]", ТипОбработчика);
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс
Функция МинимальнаяДатаИтоговРегистровНакопленияИБухгалтерии()
	
	МинимальнаяДатаИтогов = ZabbixМониторинг.МинимальнаяДатаИтоговРегистровНакопленияИБухгалтерии();	
	
	Если НЕ ЗначениеЗаполнено(МинимальнаяДатаИтогов) Тогда
		МинимальнаяДатаИтогов = '19700101';	
	Иначе
		МинимальнаяДатаИтогов = УниверсальноеВремя(МинимальнаяДатаИтогов);
	КонецЕсли; 

	МинимальнаяДатаИтоговUNIX = ДатаВремяВФорматеUnixTime(МинимальнаяДатаИтогов);
	
	МинимальнаяДатаИтоговUNIX = Формат(МинимальнаяДатаИтоговUNIX, "ЧН=0; ЧГ=0");
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	Ответ.Заголовки.Вставить("Content-Type", "text/plain");	
	Ответ.УстановитьТелоИзСтроки(МинимальнаяДатаИтоговUNIX);

	Возврат Ответ;

КонецФункции

// ECHO запрос для тестирования сервиса запросов Zabbix
// Параметры:
//	* Параметры - Структура, сформированная из тела HTTP запроса;
Функция Эхо(Параметры) Экспорт
	
	Ответ = Новый HTTPСервисОтвет(200);
	Ответ.Заголовки.Вставить("Content-Type", "Application/json");	
	Ответ.УстановитьТелоИзСтроки(Параметры);

	Возврат Ответ;
	
КонецФункции

Функция ДатаВремяВФорматеUnixTime(ПараметрДата)
	
	Результат = ПараметрДата - '19700101';
	
	Возврат Результат;
	
КонецФункции
 
#КонецОбласти