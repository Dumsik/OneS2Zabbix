Процедура ВыполнитьКоманду(ПарсерАргументов, ЗначенияПараметров) Экспорт

	ПарсерАргументов = Неопределено;

	ТипУдаленнойКоманды = ЗначенияПараметров.Получить("-type");
	ТелоЗапроса = ЗначенияПараметров.Получить("-body");
	Хост = ЗначенияПараметров.Получить("-host");
	База = ЗначенияПараметров.Получить("-db");
	Пользователь = ЗначенияПараметров.Получить("-user");
	Пароль = ЗначенияПараметров.Получить("-password");
	Таймаут = ЗначенияПараметров.Получить("-timeout");

	ПараметрыКоманды = Новый Структура;
	ПараметрыКоманды.Вставить("Тип", ТипУдаленнойКоманды);
	ПараметрыКоманды.Вставить("Тело", ТелоЗапроса);
	ПараметрыКоманды.Вставить("Хост", Хост);
	ПараметрыКоманды.Вставить("База", База);
	ПараметрыКоманды.Вставить("Пользователь", Пользователь);
	ПараметрыКоманды.Вставить("Пароль", Пароль);
	ПараметрыКоманды.Вставить("Таймаут", ?(ЗначениеЗаполнено(Таймаут), Таймаут, 30));

	Попытка
		ВызватьУдаленнуюПроцедуру(ПараметрыКоманды);
	Исключение
		Сообщить(ОбщегоНазначения.СформироватьТексОшибкиJSON(ОписаниеОшибки()));
	КонецПопытки;

КонецПроцедуры

Процедура ОписаниеКоманды(ПарсерКоманднойСтроки) Экспорт
	
	ИмяКоманды = "rpc";

	Команда = ПарсерКоманднойСтроки.ОписаниеКоманды(ИмяКоманды, "Вызов удаленной процедуры по HTTP");

	ПарсерКоманднойСтроки.ДобавитьИменованныйПараметрКоманды(Команда, 
		"-type", "наименование команды удаленной процедуры");
	ПарсерКоманднойСтроки.ДобавитьИменованныйПараметрКоманды(Команда, 
		"-body", "параметры команды в формате JSON");
	ПарсерКоманднойСтроки.ДобавитьИменованныйПараметрКоманды(Команда, 
		"-host", "адрес http сервиса");
	ПарсерКоманднойСтроки.ДобавитьИменованныйПараметрКоманды(Команда, 
		"-db", "имя базы данных");
	ПарсерКоманднойСтроки.ДобавитьИменованныйПараметрКоманды(Команда, 
		"-user", "имя пользователя");
	ПарсерКоманднойСтроки.ДобавитьИменованныйПараметрКоманды(Команда, 
		"-password", "пароль пользователя");
	ПарсерКоманднойСтроки.ДобавитьИменованныйПараметрКоманды(Команда, 
		"-timeout", "таймаут http запросов. 30 сек по умолчанию");

	ПарсерКоманднойСтроки.ДобавитьКоманду(Команда);
	
КонецПроцедуры

Процедура ВызватьУдаленнуюПроцедуру(Параметры) Экспорт

	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-type", "Application/json");

	АдресРесурса = СтрШаблон("%1/hs/zabbix/services/%2", Параметры.База, Параметры.Тип);
	Запрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
	Запрос.УстановитьТелоИзСтроки(Параметры.Тело);

	Хост = Параметры.Хост;

	ПротоколHTTP = ВРег(Лев(Хост, 7)) = "HTTP://";
	ПротоколHTTPS = ВРег(Лев(Хост, 8)) = "HTTPS://";
	ПротоколУказан = ПротоколHTTP ИЛИ ПротоколHTTPS;
	Если НЕ ПротоколУказан Тогда
		Хост = СтрШаблон("https://%1", Хост);
	КонецЕсли;

	Соединение = Новый HTTPСоединение(Хост, , Параметры.Пользователь, Параметры.Пароль, ,	Параметры.Таймаут);

	Ответ = Соединение.ОтправитьДляОбработки(Запрос);
	Если Ответ.КодСостояния <> 200 Тогда
		ВызватьИсключение СтрШаблон("код состояния %1: %2", Ответ.КодСостояния, Ответ.ПолучитьТелоКакСтроку());
	КонецЕсли;

	Сообщить(Ответ.ПолучитьТелоКакСтроку());
	
КонецПроцедуры